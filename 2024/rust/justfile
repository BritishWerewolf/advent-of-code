# List all available commands.
default:
    just --list

add package day:
    cargo add {{package}} --package {{day}}

# Create a new folder for the given day.
create day:
    if ! command -v cargo generate &> /dev/null; then cargo install cargo-generate; fi
    cargo generate --path ./daily-template --name {{day}}
    git switch -c 2024/{{day}}
    git add .
    git commit -m "Set up {{day}}."
    git push --set-upstream origin 2024/{{day}}

# Run the given day.
run day:
    cargo run --bin {{day}}

# Run tests for the given day.
test day:
    cargo test --bin {{day}}

# Merge the given day into main.
merge day:
    git push
    git switch main
    git merge --no-ff 2024/{{day}} -m "Merge branch '2024/{{day}}'"
    git push
    git push origin --delete 2024/{{day}}
    git fetch --prune
